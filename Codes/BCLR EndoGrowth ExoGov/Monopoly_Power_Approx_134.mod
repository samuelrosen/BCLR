var EV ut da argp ev ca ypa yca ya kpa kca ipa Ic G Gprim T Tprim Gamp Gampprim Gamc Gamcprim m mc dc qp qc p ptil r rc dyp dyc dyvp dip igitot exr exr_G rf vol tyg titot gammav dy pqr wagep wagec iyp ia tca dtc np nc ntotal npnt ncnt sla LC dLCdHP dLCdHG wagepc w_var ome_p x_p pi_p v_p vtheta s kratio ds x dtfp gdp dgdp rpatent exrpatent D Vexp VexpK rrnd exrrnd Iyg Tax Vexpg VexpKg dpgdp pgdp dypa dya r_H exrr_H r_H_patent exrr_H_patent coeff1 coeff2 Er Errnd Erc Er_H_patent;
varexo ea es;

parameters theta deltac mu rho rhovol rhoar psi gamma alphap deltak xi cxi alpha1 alpha0 calpha1 calpha0 philev sigma sigmax alphac beta app tau w arc con_shocka con_shockx phi_growth pss ptilss ypss yss ipss kpss kcss rhoinv phi_growth_f theta_sla omega_barp omega_barc omega Np_ss Nc_ss theta_P theta_G phi_o_s Abar xi_p nu eta chi ome_p_bar phi_p gcctrl biga bigv bnca bncv rho_ig rho_nc Iygbar ncntbar rho_ivol rho_ia rho_nvol rho_na;

mu = -0.002581;
rho = 0.980000;
rhovol = 0.740000;
rhoar = 0.990000;
arc = -0.432200;
w = 0.800000;
phi_o_s = 0.000000;
tau = 5.000000;
psi = 2.000000;
gamma = 12.000000;
theta = -0.045455;
alphap = 0.300000;
deltak = 0.015000;
xi = 8.000000;
cxi = 8.000000;
calpha1 = 1.001544;
calpha0 = -0.144632;
philev = 2.000000;
alpha1 = 0.577797;
alpha0 = -0.001775;
sigma = 0.015750;
sigmax = 0.003150;
alphac = 0.300000;
deltac = 0.015000;
app = 0.005000;
beta = 0.991133;
con_shocka = -3.500000;
con_shockx = 0.000000;
phi_growth = 0.000000;
phi_growth_f = 0.000000;
pss = 3.686429;
ptilss = 4.674557;
ypss = 0.621371;
yss = 0.549708;
ipss = -2.317032;
kpss = 2.071236;
kcss = 1.719101;
rhoinv = 0.100000;
theta_sla = 0.100000;
omega_barp = 0.349134;
omega_barc = 0.442042;
omega = 1.500000;
theta_P = 0.000000;
theta_G = 0.000000;
Np_ss = 1.000000;
Nc_ss = 1.000000;
Abar = 0.265403;
xi_p = 0.490000;
nu = 0.578512;
eta = 0.760000;
chi = 0.122000;
ome_p_bar = 1.895007;
phi_p = 0.040000;
gcctrl = 1.000000;
biga = -0.040000;
bigv = 0.030000;
bnca = -0.200000;
bncv = 0.015000;
rho_ig = 0.700000;
rho_nc = 0.850000;
Iygbar = 0.040000;
ncntbar = 0.500000;
rho_ivol = 0.000000;
rho_ia = 0.000000;
rho_nvol = 0.000000;
rho_na = 0.000000;

model;
//Time Varing Gamma and beta;
gammav = gamma;
//Utility;
EV = exp((ut(+1)+da)*(1-gammav));
exp(ev) = EV;
ut = (1/(1-1/psi))*log((1-beta)*exp(tca*(1-1/psi))+beta*exp((1-1/psi)/(1-gammav)*ev));
exp(tca) = exp(ca) - omega_barp*exp(sla)*(exp(np)+omega_barc*exp(nc))^omega/omega;
dtc = tca - tca(-1) + da(-1);
sla = (1-theta_sla)*(mu+sla(-1)-da(-1));
LC       = exp(sla)*(theta_P*( (exp(np)) - Np_ss )^2 + theta_G*( (exp(nc)) - Nc_ss )^2);
dLCdHP   = exp(sla)*2*theta_P*( (exp(np)) - Np_ss  );
dLCdHG   = exp(sla)*2*theta_G*(  (exp(nc)) - Nc_ss );
ntotal   = log(exp(np)+exp(nc));
npnt   = exp(np)/exp(ntotal);
ncnt   = exp(nc)/exp(ntotal);
//SDF and Prices;
m = log(beta)+(-1/psi)*dtc+(1/psi-gammav)*(ut+da(-1)-(1/(1-gammav))*ev(-1));
mc = log(exp(m)*exp(ya-yca)^(1/tau)/exp(ya(-1)-yca(-1))^(1/tau));
//Output and Resources;
ypa = log(Abar)+(1-alphap)*(ome_p)+ alphap*(kpa(-1))+(1-alphap)*log((exp(np)));
yca = log(Abar)+(1-alphac)*(ome_p)+ alphac*(kca(-1))+(1-alphac)*(log((exp(nc)))+argp);
//relative prices;
w_var = w*exp(phi_o_s*(vol(-1)-0));
ya = 1/(1-1/tau)*log(w_var*(exp(ypa))^(1-1/tau)+(1-w_var)*(exp(yca))^(1-1/tau));
p = w_var/(1-w_var)*(exp(ypa-yca))^(-1/tau);
ptil = 1/(1-w_var)*(exp(yca-ya))^(1/tau);
exp(ya)= exp(ca)+exp(ipa)+Ic+LC+(p/ptil)*exp(x_p)+exp(s);
exp(gdp) = exp(ya) - (p/ptil)*exp(x_p);
dgdp = gdp-gdp(-1)+da(-1);
dya= log(exp(ya)*(ptil/p))-log(exp(ya(-1))*(ptil(-1)/p(-1)))+da;
dypa= log(exp(ypa)*p)-log(exp(ypa(-1))*p(-1))+da;
exp(pgdp) = exp(ypa) - exp(x_p);
dpgdp = pgdp-pgdp(-1)+da(-1);
//Adjustment Costs G and T;
G = exp(ipa-kpa(-1))-(alpha1/(1-1/xi)*(exp(ipa-kpa(-1)))^(1-1/xi)+alpha0);
Gprim = 1-alpha1*exp(ipa-kpa(-1))^(-1/xi);
T = Ic*exp(-kca(-1))+gcctrl-(calpha1/(1-1/cxi)*(Ic*exp(-kca(-1))+gcctrl)^(1-1/cxi)+calpha0);
Tprim = 1-calpha1*(Ic*exp(-kca(-1))+gcctrl)^(-1/cxi);
Gamp = exp(ipa)/exp(kpa(-1))-G;
Gampprim = 1-Gprim;
Gamc = Ic/exp(kca(-1))-T;
Gamcprim = 1-Tprim;
//The Law of Motion of Capital;
exp(kpa+da) = (1-deltak)*exp(kpa(-1))+exp(ipa)-G*exp(kpa(-1));
exp(kca+da) = (1-deltac)*exp(kca(-1))+Ic-T*exp(kca(-1));
qp = ptil/(Gampprim);
qc = ptil/(Gamcprim);
pqr = qc/qp;
//Wages in Government Good Prices;
wagep = p*(1-1/tau)*(1-alphap)*(1-xi_p)*exp(ypa)/(exp(np));
wagepc = wagep/wagec;
wagep/ptil = omega_barp*exp(sla)*(exp(np)+omega_barc*exp(nc))^(omega-1)+dLCdHP;
wagec/ptil = omega_barp*omega_barc*exp(sla)*(exp(np)+omega_barc*exp(nc))^(omega-1)+dLCdHG;
//Returns;
exp(r) = ((1-1/tau)*p*alphap*(1-xi_p)*exp(ypa-kpa(-1))+(qp*(1-deltak)+qp*(-Gampprim*exp(ipa-kpa(-1))+Gamp)))/(qp(-1));
exp(rc) = (alphac*exp(yca-kca(-1))+(qc*(1-deltac)+qc*(-Gamcprim*Ic*exp(-kca(-1))+Gamc)))/(qc(-1));
1 = exp(mc(+1) + r(+1));
Iyg =  (1-rho_ig)*Iygbar+rho_ig*Iyg(-1)+biga*ea+bigv*es+ rho_ivol*vol(-1)+rho_ia*(ome_p(-1)-ome_p_bar);
ncnt   = (1-rho_nc)*ncntbar+rho_nc*ncnt(-1)+bnca*ea+bncv*es(-1) + rho_nvol*vol(-1)+rho_na*(ome_p(-1)-ome_p_bar);
ptil*D = p*exp(ypa)-wagep*(exp(np))-ptil*exp(ipa)-p*(1/nu)*exp(x_p);
Vexp = exp(m(+1))*(Vexp(+1)+D(+1))*exp(da(+1));
VexpK =Vexp/exp(kpa) ;
ptil*Tax = exp(yca)-wagec*(exp(nc))-ptil*Ic;
Vexpg = exp(m(+1))*(Vexpg(+1)+Tax(+1))*exp(da(+1));
VexpKg =Vexpg/exp(kca) ;
//Innovation;
x_p = (1/(1-xi_p))*log(xi_p*nu*(1-1/tau)/(1-xi_p/tau)) + (1-alphap)*(ome_p)+ alphap*(kpa(-1))+(1-alphap)*log((exp(np)));
pi_p = log(1/nu-1) + x_p +log(p);
exp(v_p) = exp(pi_p) + (1-phi_p)*exp(mc(+1) + v_p(+1));
exp(rpatent) = (1-phi_p)*exp(v_p)/(exp(v_p(-1))-exp(pi_p(-1)));
exp(da) = exp(vtheta+s) + 1-phi_p;
vtheta = log(chi) + (eta-1)*s;
ptil*exp(s) = exp(mc(+1) + v_p(+1))*(exp(da) - (1-phi_p));
rrnd = log((exp(v_p)*(exp(da(-1)) - (1-phi_p)))/(ptil(-1)*exp(s(-1))));
//Productivity;
    ome_p = (1-rho)*ome_p_bar+ rho*ome_p(-1) + exp(vol(-1))*ea;
vol = rhovol*vol(-1)+ con_shocka*ea+con_shockx*(da-da(-1))+es;
    dtfp = (rho-1)*(ome_p(-1)-ome_p_bar)+da(-1)+ exp(vol(-1))*ea;
    x = (rho-1)*(ome_p-ome_p_bar)+da;
argp = (1-rhoar)*argp(-1)+(1-rhoar)*(mu-da(-1))+rhoar*arc;
//Growth;
dc = ca - ca(-1) + da(-1);
kratio = exp(kca)/(exp(kca)+exp(kpa)) ;
dyp = ypa - ypa(-1) + da(-1);
dip = ipa - ipa(-1) + da(-1);
ds = s - s(-1) + da(-1);
dyc = yca - yca(-1) + da(-1);
igitot = Ic/(exp(ipa)+Ic+exp(s));
dy = ya-ya(-1)+da(-1);
tyg = (ypa+yca) - (ypa(-1)+yca(-1)) + da(-1);
titot = log((exp(ipa)+Ic+exp(s))/(exp(ipa(-1))+Ic(-1)+exp(s(-1)))*exp(da(-1)));
dyvp = log(exp(ypa)*p/(exp(ypa(-1))*p(-1))) + da(-1);
iyp =  ipa-ya;
ia =  log((exp(ipa)+Ic));
Iyg =  Ic/exp(ya);
//Excess Rate of Returns;
1/exp(rf) = exp(m(+1));
exr = log(exp(r)*exp(ya-yca)^(1/tau)/exp(ya(-1)-yca(-1))^(1/tau))-rf(-1);
exr_G = log(exp(rc)*exp(ya-yca)^(1/tau)/exp(ya(-1)-yca(-1))^(1/tau))-rf(-1);
exrpatent = rpatent-rf(-1);
exrrnd = rrnd-rf(-1);
r_H = log((qp(-1)*exp(kpa(-1))*exp(r)+exp(v_p(-1))*exp(rrnd))/(qp(-1)*exp(kpa(-1))+exp(v_p(-1))));
exrr_H = r_H-rf(-1);
coeff1 = qp(-1)*exp(kpa(-1))/(qp(-1)*exp(kpa(-1))+exp(v_p(-1)));
coeff2 = exp(v_p(-1))/(qp(-1)*exp(kpa(-1))+exp(v_p(-1)));
r_H_patent = log((qp(-1)*exp(kpa(-1))*exp(r)+exp(v_p(-1))*exp(rpatent))/(qp(-1)*exp(kpa(-1))+exp(v_p(-1))));
exrr_H_patent = r_H_patent-rf(-1);
Er = r(+1);
Errnd = rrnd(+1);
Erc = rc(+1);
Er_H_patent = r_H_patent(+1);
end;

initval;
gammav=12.000000000000;
ca=-0.015525186021;
m=-0.007616242084;
mc=-0.007616242084;
da=-0.002581104653;
argp=-0.432200000000;
ut=-0.811420026010249;
ya=0.549708360104;
gdp=0.310180919819;
pgdp=0.331799473972;
dgdp=-0.002581104653;
dypa=-0.002581104653;
dya=-0.002581104653;
dpgdp=-0.002581104653;
ypa=0.621370779279;
yca=0.213190262691;
kpa=2.071235930930;
kca=1.719100875634;
kratio=0.412864771662;
ipa=-2.317032259083;
Ic=0.069309904220;
wagep=1.959846176068;
wagec=0.866334071242;
wagepc=2.262229134377;
G=0.000000000000;
Gprim = 0.000000000000;
Gamp=0.012422223534;
Gampprim = 1.000000000000;
T=0.000000000000;
Tprim = 0.000000000000;
Gamc=0.012422221663;
Gamcprim = 1.000000000000;
dc=-0.002581104653;
dyp=-0.002581104653;
dyvp=-0.002581104653;
dyc=-0.002581104653;
dy=-0.002581104653;
tyg=-0.002581104653;
dip=-0.002581104653;
ds=-0.002581104653;
igitot=0.182838542518;
titot=-0.002581104653;
iyp=-2.866740619187;
ia=-1.784532207522;
tca=-0.542082203330;
dtc=-0.002581104653;
np=0.000000000000;
nc=0.000000000000;
ntotal=0.693147180560;
npnt=0.500000000000;
ncnt=0.500000000000;
Iyg=0.039999999992;
sla=0.000000000000;
LC=0.000000000000;
dLCdHP=0.000000000000;
dLCdHG=0.000000000000;
w_var=0.800000000000;
p=3.686429070043;
ptil=4.674556503046;
qp=4.674556503046;
qc=4.674556503046;
r=0.007616242084;
rc=-0.000764810553;
rf=0.007616242084;
rpatent=0.007616242084;
rrnd=0.007616242084;
r_H=0.007616242084;
r_H_patent=0.007616242084;
Errnd=0.007616242084;
Er=0.007616242084;
Erc=-0.000764810553;
Er_H_patent=0.007616242084;
exr=0.000000000000;
exr_G=-0.008381052637;
exrpatent=0.000000000000;
exrrnd=0.000000000000;
exrr_H=0.000000000000;
exrr_H_patent=0.000000000000;
vol = 0.000000000000;
EV = 7738.881424122921089;
ev = 8.954012437292750;
pqr=1.000000000000;
ome_p=1.895007036615;
x_p=-0.759277204540;
pi_p=0.228711443937;
v_p=3.280298567864;
vtheta=-1.730548032783;
s=-1.554942506106;
dtfp=-0.002581104653;
x=-0.002581104653;
D=-0.090624;
Vexp=-8.841738;
VexpK=-1.114324;
Tax=0.010117;
Vexpg=0.987081;
VexpKg=0.176912;
coeff1=0.582507;
coeff2=0.417493;
end;

vcov = [0.000248062500 0 ;
       0        0.023654440000] ;

order = 3;
